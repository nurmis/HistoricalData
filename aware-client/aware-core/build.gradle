apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = version_readable
group = "com.awareframework"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        versionCode version_code
        versionName version_readable
        targetSdkVersion 23
        minSdkVersion 10 //Android 2.3.3
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(project.hasProperty("KEYSTORE_FILE") ? KEYSTORE_FILE : "/")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias project.hasProperty("KEY") ? KEY : ""
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : ""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = project.hasProperty("bintrayUsername") ? bintrayUsername : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = 'com.aware'
        licenses = ['Apache-2.0']
        websiteUrl = 'http://www.awareframework.com'
        issueTrackerUrl = 'https://github.com/denzilferreira/aware-client/issues'
        labels = ['aar', 'android', 'framework', 'aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/denzilferreira/aware-client.git'
        desc = "AWARE Framework common library"
        userOrg = user

        githubRepo = "denzilferreira/aware-client"
        githubReleaseNotesFile = "README.md"
        version {
            name = "$version_readable"
            desc = bintray.pkg.desc
            released = new Date()
            vcsTag = "$version_readable"
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

dependencies {
    compile "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2"
    compile "com.koushikdutta.ion:ion:2.1.6"
    compile "com.android.support:cardview-v7:$support_libs"
    compile "com.android.support:appcompat-v7:$support_libs"
    compile "com.android.support:design:$support_libs"

    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'

    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'joda-time:joda-time:2.9.2'
    compile 'mysql:mysql-connector-java:5.1.17'
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
}

